## vim:ft=zsh
## Written by Frank Terbeck <ft@bewatermyfriend.org>
## Distributed under the same BSD-ish license as zsh itself.

_vcs_dir_realpath()
{
    setopt localoptions NO_shwordsplit chaselinks
    # -q to avoid chpwd() invocations
    ( builtin cd -q $1 2>/dev/null && pwd; )
}

setopt localoptions NO_shwordsplit
local basedir="." realbasedir

realbasedir="$(_vcs_dir_realpath ${basedir})"
vcs_dir_type=
while [[ ${realbasedir} != '/' ]]; do
    [[ -r ${realbasedir} ]] || return 1
    if [[ -d ${basedir}/.hg ]]; then
        vcs_dir_type=hg
        break
    elif [[ -d ${basedir}/.git ]]; then
        vcs_dir_type=git
        break
    fi

    basedir=${basedir}/..
    realbasedir="$(_vcs_dir_realpath ${basedir})"
done

if [[ ${realbasedir} == "/" ]]; then
    vcs_dir_path=
    return 1
fi
vcs_dir_path=${realbasedir}
return 0
